添加一个类模块，涉及枚举变量Enum

Option Explicit

Public Enum SourType
    WorksheetRange = 1
    TwoDimentionArray = 2
    OneDimentionArray = 3
End Enum




Public Enum RefName
    File_Folder_Dictionary = 1
    Access_Connection = 2
    IE_Browser = 3
    IE_HTMLElements = 4
    WordApplication = 5
    OutlookApplication = 6
End Enum

Private Function GenerateRefArray(ByVal ReferenceName As RefName) As String()

Dim TempArray() As String
ReDim TempArray(2) As String
Select Case ReferenceName
Case File_Folder_Dictionary
    TempArray(0) = "{420B2830-E718-11CF-893D-00A0C9054228}"
    TempArray(1) = "1"
    TempArray(2) = "0"
Case Access_Connection
    TempArray(0) = "{B691E011-1797-432E-907A-4D8C69339129}"
    TempArray(1) = "6"
    TempArray(2) = "1"
Case IE_Browser
    TempArray(0) = "{EAB22AC0-30C1-11CF-A7EB-0000C05BAE0B}"
    TempArray(1) = "1"
    TempArray(2) = "1"
Case IE_HTMLElements
    TempArray(0) = "{3050F1C5-98B5-11CF-BB82-00AA00BDCE0B}"
    TempArray(1) = "4"
    TempArray(2) = "0"
Case WordApplication
    TempArray(0) = "{00020905-0000-0000-C000-000000000046}"
    TempArray(1) = "8"
    TempArray(2) = "7"
Case OutlookApplication
    TempArray(0) = "{00062FFF-0000-0000-C000-000000000046}"
    TempArray(1) = "9"
    TempArray(2) = "6"
Case Else
    MsgBox "This reference name is unknow to this EnvironmentHandler."
End Select
GenerateRefArray = TempArray()

End Function



Public Sub ListAllReference()

Dim i As Long
Dim Ref As Variant
For Each Ref In ThisWorkbook.VBProject.References
    i = i + 1
    Debug.Print i & "_" & Ref.Name & "_" & Ref.GUID & "_" & Ref.Major & "_" & Ref.Minor
Next Ref

End Sub


Public Sub AddReference(ByVal ReferenceName As RefName)

Dim RefArray As Variant
RefArray = GenerateRefArray(ReferenceName)

Dim RefFound As Boolean
Dim Ref As Variant
For Each Ref In ThisWorkbook.VBProject.References
    If Ref.GUID = RefArray(0) Then
        RefFound = True
        Exit For
    End If
Next Ref
If RefFound = False Then
    ThisWorkbook.VBProject.References.AddFromGuid _
        GUID:=RefArray(0), Major:=RefArray(1), Minor:=RefArray(2)
End If

End Sub


